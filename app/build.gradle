buildscript {
  repositories {
    jcenter()
  }
}

repositories {
  maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
  mavenCentral()
}

apply plugin: "com.android.application"
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

repositories {
  maven { url "https://jitpack.io" }
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

final def signingPropertiesFile = rootProject.file("signing/keystore.properties")
final def enableReleaseSigning = signingPropertiesFile.exists()

android {
  compileSdkVersion versions.compileSdk

  defaultConfig {
    applicationId "me.thanel.dank"
    minSdkVersion versions.minSdk
    targetSdkVersion versions.compileSdk
    versionCode 19
    versionName "0.9.3"
    multiDexEnabled true
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    debug {
      storeFile rootProject.file("signing/app-debug.jks")
      storePassword "android"
      keyAlias "androiddebugkey"
      keyPassword "android"
    }

    if (enableReleaseSigning) {
      final def signingProperties = new Properties()
      signingProperties.load(new FileInputStream(signingPropertiesFile))

      release {
        keyAlias signingProperties['keyAlias']
        keyPassword signingProperties['keyPassword']
        storeFile rootProject.file("signing/app-release.jks")
        storePassword signingProperties['storePassword']
      }
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      if (enableReleaseSigning) {
        signingConfig signingConfigs.release
      }
    }
  }

  compileOptions {
    targetCompatibility JavaVersion.VERSION_1_8
    sourceCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE-LGPL-3.txt'
    exclude 'META-INF/LICENSE-LGPL-2.1.txt'
    exclude 'META-INF/LICENSE-W3C-TEST'
    exclude 'META-INF/rxjava.properties'
  }

  dexOptions {
    preDexLibraries true
    maxProcessCount 8
  }

  lintOptions {
    disable 'CheckResult'
    quiet true
    abortOnError false
    ignoreWarnings true
  }

  androidExtensions {
    experimental = true
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

task printVersionName {
  doLast {
    println android.defaultConfig.versionName
  }
}

dependencies {
  implementation project(path: ':markdownhints')
  implementation project(path: ':linkunfurler')

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

  testImplementation "junit:junit:$versions.junit"
  testImplementation 'org.powermock:powermock-module-junit4:2.0.4'
  testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
  testImplementation "com.google.truth:truth:1.0.1"
  testImplementation 'org.robolectric:robolectric:4.3'
  testImplementation 'androidx.test:core:1.2.0'
  testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

  androidTestImplementation 'androidx.annotation:annotation:1.1.0'
  androidTestImplementation 'androidx.test:runner:1.2.0'
  androidTestImplementation 'androidx.test:rules:1.2.0'

  implementation 'androidx.recyclerview:recyclerview:1.0.0'
  implementation 'androidx.annotation:annotation:1.1.0'
  implementation 'com.google.android.material:material:1.0.0'
  implementation 'androidx.browser:browser:1.2.0'
  implementation 'androidx.palette:palette:1.0.0'

  implementation "com.squareup.moshi:moshi:$versions.moshi"
  kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshi"
  kapt "com.google.auto.value:auto-value:$versions.autoValue"
  api "com.jakewharton.auto.value:auto-value-annotations:$versions.autoValue"
  kapt "com.ryanharter.auto.value:auto-value-moshi:$versions.autoValueMoshi"
  api "com.ryanharter.auto.value:auto-value-moshi-annotations:$versions.autoValueMoshi"
  kapt 'com.ryanharter.auto.value:auto-value-parcel:0.2.8'
  implementation "com.jakewharton.timber:timber:$versions.timber"
  implementation "com.jakewharton:butterknife:$versions.butterKnife"
  kapt "com.jakewharton:butterknife-compiler:$versions.butterKnife"
  implementation "io.reactivex.rxjava2:rxjava:$versions.rxjava"
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
  implementation "com.jakewharton.rxbinding2:rxbinding:$versions.rxBindings"
  implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$versions.rxBindings"
  implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$versions.rxBindings"
  implementation "com.jakewharton.rxrelay2:rxrelay:2.1.1"
  implementation 'com.github.qoqa:traceur:2.2.12'
  implementation "com.google.dagger:dagger:$versions.dagger"
  kapt "com.google.dagger:dagger-compiler:$versions.dagger"
  implementation "com.squareup.okhttp3:okhttp:$versions.okHttp"
  implementation "com.squareup.okhttp3:logging-interceptor:$versions.okHttp"
  implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
  implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
  implementation "com.github.bumptech.glide:glide:$versions.glide"
  kapt "com.github.bumptech.glide:compiler:$versions.glide"
  implementation "com.github.bumptech.glide:okhttp3-integration:$versions.glide"
  implementation 'com.squareup.sqlbrite2:sqlbrite:2.0.0'
  implementation 'androidx.room:room-runtime:2.2.3'
  kapt 'androidx.room:room-compiler:2.2.3'
  implementation 'androidx.room:room-rxjava2:2.2.5'
  implementation 'com.google.android:flexbox:2.0.1'
  implementation 'com.github.zagum:Android-ExpandIcon:1.1.1'
  implementation 'me.saket:better-link-movement-method:2.2.0'
  implementation "org.jsoup:jsoup:$versions.jsoup"
  implementation 'com.github.rahatarmanahmed:circularprogressview:2.5.0'
  implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
  implementation 'it.gilvegliach.android:transparent-text-textview:1.0.3'
  implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'

  // TODO minSdkVersion 23: TextView supports undo on API 23+.
  // Update: Someone on ASG from the platform team said otherwise.
  implementation 'com.werdpressed.partisan:rundo:1.0.5'

  implementation 'com.jakewharton:disklrucache:2.0.2'
  implementation "com.airbnb:deeplinkdispatch:$versions.deeplinkDispatch"
  kapt "com.airbnb:deeplinkdispatch-processor:$versions.deeplinkDispatch"
  implementation 'io.github.inflationx:viewpump:2.0.3'
  implementation 'com.bugsnag:bugsnag-android:4.22.2'

  implementation 'com.github.saketme:markwon:b68d3058ca'
  implementation 'com.atlassian.commonmark:commonmark-ext-autolink:0.14.0'

  // TODO: Replace all Date4J usages with ThreeTenAbp.
  implementation 'com.darwinsys:hirondelle-date4j:1.5.1'
  implementation "org.threeten:threetenbp:1.3.6:no-tzdb"
  implementation "com.gabrielittner.threetenbp:lazythreetenbp:0.8.0"

  implementation 'com.github.saket:jraw:f8350f6558f'
  implementation("net.dean.jraw:JRAW-Android:1.1.0") {
    exclude group: 'net.dean.jraw'
  }
  implementation 'com.alexvasilkov:gesture-views:2.3.1'
  implementation 'com.github.saketme:exomedia:530dfac84c'
  implementation 'com.danikula:videocache:2.7.1'

  // Caching.
  implementation "com.nytimes.android:cache3:$versions.nytStore"
  implementation "com.nytimes.android:store3:$versions.nytStore"
  implementation "com.nytimes.android:middleware-moshi3:$versions.nytStore"
  implementation "com.nytimes.android:filesystem3:$versions.nytStore"
  implementation "com.facebook.stetho:stetho:$versions.stetho"
  implementation "com.facebook.stetho:stetho-okhttp3:$versions.stetho"
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}

// https://github.com/google/dagger/issues/1449#issuecomment-495404186
if (project.hasProperty('kapt')) {
  // Reference for 'kapt' DSL: https://kotlinlang.org/docs/reference/kapt.html#java-compiler-options
  kapt {
    // we expect this closure to run over a org.jetbrains.kotlin.gradle.plugin.KaptExtension
    javacOptions {
      option("-source", "8")
      option("-target", "8")
    }
  }
}
